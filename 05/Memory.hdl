// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O.
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory
 * location specified by address. If load==1, the in value is loaded
 * into the memory location specified by address. This value becomes
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used.
 * Access to address>0x6000 is invalid. Access to any address in
 * the range 0x4000-0x5FFF results in accessing the screen memory
 * map. Access to address 0x6000 results in accessing the keyboard
 * memory map. The behavior in these addresses is described in the
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    And(a=address[14], b=true, out=useIo);
    Not(in=useIo, out=useRam);

    And(a=address[13], b=true, out=preferKeyboard);
    Not(in=preferKeyboard, out=preferScreen);
    And(a=useIo, b=preferKeyboard, out=useKeyboard);
    And(a=useIo, b=preferScreen, out=useScreen);

    And(a=useRam, b=load, out=loadInRam);
    And(a=useScreen, b=load, out=loadInScreen);
    And(a=useKeyboard, b=load, out=loadInKeyboard);

    RAM16K(in=in, load=loadInRam, address=address[0..13], out=ramOut);
    Screen(in=in, load=loadInScreen, address=address[0..12], out=screenOut);
    Keyboard(out=keyboardOut);

    // Sel:
    // 01 - RAM
    // 00 - Keyboard
    // 10 - Screen
    Mux4Way16(
        sel[0]=useRam,
        sel[1]=useScreen,
        a=keyboardOut,
        b=ramOut,
        c=screenOut,
        out=out
    );
}